project.group = 'com.hubrick'
project.version = '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.owasp:dependency-check-gradle:1.3.6")
    }
}

group = 'hubrick'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'pmd'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'maven'

jar {
    baseName = 'reports-demo'
    version = project.version
    manifest {
        attributes 'Main-Class': 'Application'
    }
}

run {
    mainClassName = "Application"
    if ( project.hasProperty("appArgs") ) {
        args(appArgs.split(','))
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 1, 'minutes'
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile("junit:junit:4.12")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

// Permanently prevents 'UP-TO-DATE' on 'test' task
test.outputs.upToDateWhen { false }

test {
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task preRelease(dependsOn: ["clean", "dependencyCheck", "check"]) {
    doLast {
        println "Pre-release checks completed, please check the \'/build/reports\' directory to determine if the code meets the standard required."
    }
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

defaultTasks "run"
